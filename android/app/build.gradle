apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

def rootCMakeLists = "../../CMakeLists.txt"
def rootCMakeListsContent = file(rootCMakeLists).text

static def grepVersionComponent(content, varname) {
    def match = content =~ /set\($varname ([0-9]+)\)/
    if(!match.find()) throw new GradleException("Failed to find $varname in CMakeLists.txt")
    return match.group(1)
}

def chiakiVersionMajor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MAJOR")
def chiakiVersionMinor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MINOR")
def chiakiVersionPatch = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_PATCH")
def chiakiVersion = "$chiakiVersionMajor.$chiakiVersionMinor.$chiakiVersionPatch"

println("Determined Chiaki Version: $chiakiVersion")

def cmakeArgsProp = (project.findProperty("cmakeArgs") ?: "").trim()
def cmakeArgsList = cmakeArgsProp.isEmpty() ? [] : cmakeArgsProp.split("\\s+").findAll { it }

android {
    namespace "com.metallic.chiaki"
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.metallic.chiaki"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 11
        versionName chiakiVersion

        // Explicit ABI filters (matches gradle.properties)
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }

        // Place CMake arguments here (AGP exposes 'arguments' only at this level or per buildType)
        externalNativeBuild {
            cmake {
                arguments += [
                    "-DCHIAKI_ENABLE_ANDROID=ON",
                    "-DCHIAKI_ENABLE_TESTS=OFF",
                    "-DCHIAKI_ENABLE_CLI=OFF",
                    "-DCHIAKI_ENABLE_GUI=OFF",
                    "-DCHIAKI_ENABLE_SETSU=OFF",
                    "-DCHIAKI_ENABLE_FFMPEG_DECODER=OFF",
                    "-DCHIAKI_ENABLE_PI_DECODER=OFF",
                    "-DCHIAKI_ENABLE_STEAM_SHORTCUT=OFF",
                    "-DCHIAKI_ENABLE_STEAMDECK_NATIVE=OFF",
                    "-DCHIAKI_LIB_ENABLE_OPUS=OFF",
                    "-DCHIAKI_LIB_OPENSSL_EXTERNAL_PROJECT=OFF",
                    "-DCURL_DISABLE_NGHTTP2=ON",
                    "-DCURL_DISABLE_LIBSSH2=ON",
                    "-DCURL_DISABLE_LIBPSL=ON",
                    "-DCURL_DISABLE_LIBIDN2=ON",
                    "-DPROTOBUF_PROTOC_EXECUTABLE=/usr/bin/protoc",
                    "-Dnanopb_DIR=${project.rootDir}/../third-party/nanopb/install/lib/cmake/nanopb",
                    "-DOPENSSL_ROOT_DIR=${project.rootDir}/../openssl-android",
                    "-DOPENSSL_INCLUDE_DIR=${project.rootDir}/../openssl-android/include",
                    "-DOPENSSL_CRYPTO_LIBRARY=${project.rootDir}/../openssl-android/lib/libcrypto.a",
                    "-DOPENSSL_SSL_LIBRARY=${project.rootDir}/../openssl-android/lib/libssl.a"
                ]
            }
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    // Only path/version belong here
    externalNativeBuild {
        cmake {
            path rootCMakeLists
            version "3.22.1"
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                          'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    Properties properties = new Properties()
    def propertiesFile = file("../local.properties")
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }
    if(properties.containsKey("chiakiKeystore")) {
        println("Enabling Local Signing")
        buildTypes.release.signingConfig = signingConfigs.release
        buildTypes.debug.signingConfig = signingConfigs.release
        signingConfigs.release.storeFile = file(properties.get("chiakiKeystore"))
        signingConfigs.release.storePassword = properties.get("chiakiKeystorePW")
        signingConfigs.release.keyAlias = properties.get("chiakiKeyAlias")
        signingConfigs.release.keyPassword = properties.get("chiakiKeyPW")
    } else {
        println("Signing not enabled")
    }
}

// Merge injected cmakeArgs from CI into the defaultConfig arguments
if (!cmakeArgsList.isEmpty()) {
    android.defaultConfig.externalNativeBuild.cmake.arguments += cmakeArgsList
    println("Injected cmakeArgs: " + cmakeArgsList)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalUnsignedTypes"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.4'

    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    implementation "com.squareup.moshi:moshi:1.15.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.15.0"
}
